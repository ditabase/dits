// Gets local machine config
getConfig();

// Get languages, can also import from raw.githubusercontent.com
pull JavaScript, Lua, Python from '/home/isaiah/general/dits/langs/commonLangs.dit';

sig JavaScript Num func add(Num n1, Num n2) {|
    <|return (|<|n1;|> + <|n2;|>|);|>;
|}

sig Python listOf Num func addNums(listOf Num num_pairs) {|
    # we are given this: [[4,5], [2,6], [2,1]]
    # we should return this: [9, 8, 3]
    nums = <|num_pairs;|>
    result = []
    for pair in nums:
        result.append(<|add((|pair[0]|), (|pair[1]|));|>)
    <|return (|result|);|>
|}

sig Lua listOf Num func run() {|
    json = require("rxi-json-lua")
    nums = {}
    math.randomseed(os.time())
    for i = 1, 50, 1 do
        num1 = math.floor(math.random()*10)+1
        num2 = math.floor(math.random()*10)+1
        nums[i] = { [1] = num1, [2] = num2 }
    end
    
    final = json.encode(nums)
    <|listOf Num sums = addNums((|final|));|>
    -- we generated [[4,5], [2,6], [2,1]]
    print("Numbers we generated in Lua: ")
    print(final)
    <|return sums;|> -- python and JS added: [9, 8, 3]
|}

listOf Num finalNums = run();
print("Numbers looped and parsed in Python and added in JavaScript");
print(finalNums);

/* Output:
Numbers we generated in Lua: 
[[10,6],[10,5],[7,7],[9,1],[9,10]]
Numbers looped and parsed in Python and added in JavaScript
[16, 15, 14, 10, 19]
*/